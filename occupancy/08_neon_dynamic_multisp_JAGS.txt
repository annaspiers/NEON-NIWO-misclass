model {

  # Priors
  for (i in 1:2) {
    psi_beta[i]   ~ dunif(0, 20)
    p_beta[i]     ~ dunif(0, 20)
    phi_beta[i]   ~ dunif(0, 20)
    gamma_beta[i] ~ dunif(0, 20)
  }
  
  # Parameter priors vary across species
  for (j in 1:nspec) {
    psi1[j] ~ dbeta(psi_beta[1], psi_beta[2])     #- occupancy prob.
    p[j]    ~ dbeta(p_beta[1], p_beta[2])         #- detection prob.
    phi[j]  ~ dbeta(phi_beta[1], phi_beta[2])     #- survival prob.
    gamma[j]~ dbeta(gamma_beta[1], gamma_beta[2]) #- colonization prob.
  }
  
  # State model
  # z dimensions: [ntrap, nspec, nyear]
  for (i in 1:ntrap) {
    for (j in 1:nspec) {
      z[i,j,1]  ~ dbern(psi1[j])
      for (l in 2:nyear) {
        z[i,j,l]    ~ dbern(z[i,j,l-1]*phi[j] + 
                        (1 - z[i,j,l-1])*gamma[j]) 
      }
    }
  }

  # observation model
  for (i in 1:ntrap) {
    for (j in 1:nspec) {
      for (k in 1:nsurv) {
        for (l in 1:nyear) {
          y[i,j,k,l]    ~ dbern(z[i,j,l]*p[j])
        }
      }
    }
  }
  
  # Derived parameters
  for (j in 1:nspec) {
    psi[j,1] <- psi1[j]
    n.occ[j,1]  <- sum(z[1:ntrap,j,1])
    for (l in 2:nyear) {
      psi[j,l]        <- psi[j,l-1]*phi[j] + 
                          (1 - psi[j,l-1])*gamma[j] 
      n.occ[j,l]      <- sum(z[1:ntrap,j,l])
      growth[j,l]     <- psi[j,l]/psi[j,l-1] 
      turnover[j,l-1] <- (1 - psi[j,l-1])*gamma[j]/psi[j,l] 
    }
  }
  
  
} #model

