model {

  # Priors
  psi_beta[1]   ~ dunif(0,20) 
  psi_beta[2]   ~ dunif(0,20) 
  lambda_shape  ~ dgamma(5,1) 
  lambda_rate   ~ dgamma(1,1)
  
  # each species has its own occupancy prob, classification prob vector, and expected number of detections
  for (k in 1:nspec) {
    psi[k] ~ dbeta(psi_beta[1], psi_beta[2]) 
    theta[k,1:nspec] ~ ddirch(alpha[k,1:nspec])
    for (j in 1:nsurv) {
      for (i in 1:nsite) {
        lambda[i,j,k] ~ dgamma(lambda_shape,lambda_rate)
      }
    }
  }
  
  # State model
  for (k in 1:nspec) {
    for (i in 1:nsite) {
      z[i,k] ~ dbern(psi[k])
    }
  }

  # Observation model
  for (k in 1:nspec) {
    M[k,] ~ dmulti(theta[k, ], n[k]) #n captures expert tax's efforts
    #only deals with classification. doesn't tell us anything about occupancy. Theta links the misclassification to the occupancy model
  }
  for (i in 1:nsite) {
    for (j in 1:nsurv) {
      for (k_prime in 1:nspec) {
        c_obs[i,j,k_prime] ~ dpois( sum(z[i, ] * lambda[i,j, ] * theta[ ,k_prime]) )
      }
	  }
  }
} #model
